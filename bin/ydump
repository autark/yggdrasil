#!/usr/bin/perl

use strict;
use warnings;

use MIME::Base64;
use Getopt::Long;

use FindBin qw($Bin);
use lib qq($Bin/../lib);

use Yggdrasil;

my ($user, $password, $host, $port, $db, $engine, $NAMESPACE, $VERSION) =
  ($ENV{YGG_USER}, $ENV{YGG_PASSWORD}, $ENV{YGG_HOST}, $ENV{YGG_PORT}, $ENV{YGG_DB}, $ENV{YGG_ENGINE}, 'Ygg', '0.01');

GetOptions(
	   "user=s"       => \$user,
	   "engine=s"     => \$engine,
	   "password=s"   => \$password,
	   "host=s"       => \$host,
	   "database=s"   => \$db,
	   "engine=s"     => \$engine,
	   "port=s"       => \$port,
	   "help"         => \&help,
	   "version"      => \&version,
	   "namespace=s"  => \$NAMESPACE,
    );

new Yggdrasil(
	      user      => $user,
	      password  => $password,
	      host      => $host,
	      port      => $port,
	      db        => $db,
	      engine    => $engine,
	      namespace => $NAMESPACE,
	      admin     => 1,
	     );

# --- MetaEntity
print "[MetaEntity]\n";
my %seen_entity;
my $entities = Yggdrasil::MetaEntity->admin_dump();
foreach my $entry ( @$entities ) {
    dumper( $entry );
    print "\n";

    my $entity = $entry->{entity};
    $seen_entity{$entity} = 1;
}
print "\n";

# --- MetaProperty
print "[MetaProperty]\n";
my %seen_property;
my $properties = Yggdrasil::MetaProperty->admin_dump();
foreach my $entry ( @$properties ) {
    dumper( $entry, exclude => { id => 1 } );
    print "\n";

    my $entity   = $entry->{entity};
    my $property = $entry->{property};
    $seen_property{$entity}->{$property} = 1;
}
print "\n";

# --- MetaRelation
print "[MetaRelation]\n";
my %seen_relation;
my $relations = Yggdrasil::MetaRelation->admin_dump();
foreach my $entry ( @$relations ) {
    dumper( $entry );
    print "\n";

    my $relation = $entry->{relation};
    $seen_relation{$relation} = 1;
}
print "\n";

my %id_map;
# --- Instance Entities
foreach my $entity ( keys %seen_entity ) {
    print "[Entity: ", encode_base64($entity, ""), "]\n";
    
    my $instances = Yggdrasil::Entity->admin_dump( $entity );
    foreach my $instance ( @$instances ) {
	$id_map{$entity}->{$instance->{id}} = $instance->{visual_id};
	dumper($instance, exclude => { id => 1 } );
    }
    print "\n";
}

# --- Instance Properties
foreach my $entity ( keys %seen_property ) {
    my $properties = $seen_property{$entity};
    foreach my $property ( keys %$properties ) {
	print "[Property: ", encode_base64($entity, ""), " ", encode_base64($property, ""), "]\n";

	my $instance_property = Yggdrasil::Property->admin_dump( $entity, $property );
	foreach my $ip ( @$instance_property ) {
	    dumper($ip, 
		   usemap => { instance => $id_map{$entity} }, 
		   map    => { id => "instance" } );
	    print "\n";
	}
    }
}

# --- Instance Relations
foreach my $relation ( keys %seen_relation ) {
    my $instance_relation = Yggdrasil::Relation->admin_dump( $relation );
    my( $e1, $e2 ) = split /_R_/, $relation;
    print "[Relation: ", encode_base64($relation, ""), "]\n";
    
    foreach my $ir ( @$instance_relation ) {
	dumper($ir, usemap => { lval => $id_map{$e1}, rval => $id_map{$e2} },
	       map => { rval => "rval", lval => "lval" },
	       exclude => { id => 1 } );
	print "\n";
    }
}

sub dumper {
    my $data  = shift;
    my %param = @_;

    my $exclude = $param{exclude} || {};
    my $usemap  = $param{usemap} || {};
    my $map     = $param{map} || {};
    
    foreach my $key ( keys %$data ) {
	next if $exclude->{$key};

	my $value = $data->{$key};

	if( exists $map->{$key} ) {
	    $value = $usemap->{ $map->{$key} }->{$value};
	}

	$value = defined $value ? encode_base64($value, "") : "";
	print "$key => $value\n";
    }
}

sub help {
    print <<USAGE;
Usage: $0

Dumps entire Yggdrasil databases to standard out. Use yrestore to
recreate the database.

USAGE

    print <DATA>;
    exit;
}

sub version {
    print "ydump version $VERSION\n";
    exit;
}

__DATA__
Options:
--------
--help               Prints out usage help
--version            Prints version information
--user <name>        Specify database login name
--password <pass>    Specify database password
--host <host>        Specify database host
--port <port>        Specify database port
--db <name>          Specify database name
--engine <name>      Specify database engine
--namespace <space>  Specify Yggdrasil namespace
