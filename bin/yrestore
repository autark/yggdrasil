#!/usr/bin/perl

use strict;
use warnings;

use MIME::Base64;
use Getopt::Long;

use FindBin qw($Bin);
use lib qq($Bin/../lib);

use Yggdrasil;

our %ID_MAP;

my ($user, $password, $host, $port, $db, $engine, $mapper, $NAMESPACE, $VERSION) =
  ($ENV{YGG_USER}, $ENV{YGG_PASSWORD}, $ENV{YGG_HOST}, $ENV{YGG_PORT}, $ENV{YGG_DB}, $ENV{YGG_ENGINE}, undef, 'Ygg', '0.01');

GetOptions(
	   "mapper=s"     => \$mapper,
	   "user=s"       => \$user,
	   "engine=s"     => \$engine,
	   "password=s"   => \$password,
	   "host=s"       => \$host,
	   "database=s"   => \$db,
	   "engine=s"     => \$engine,
	   "port=s"       => \$port,
	  );

new Yggdrasil(
	      user      => $user,
	      password  => $password,
	      host      => $host,
	      port      => $port,
	      db        => $db,
	      engine    => $engine,
	      mapper    => $mapper,
	      namespace => $NAMESPACE,
	      admin     => 1,
	     );

my $store = {
    MetaEntity   => \&meta_entity,
    MetaProperty => \&meta_property,
    MetaRelation => \&meta_relation,
    MetaInheritance => \&meta_inheritance,
    Entity   => \&entity,
    Property => \&property,
    Relation => \&relation,
};

my $define = {
    MetaEntity   => sub {},
    MetaProperty => sub {},
    MetaRelation => sub {},
    MetaInheritance => sub {},
    Entity   => sub{},
    Property => \&property_define,
    Relation => \&relation_define,
};


my( @data, $section, $extra );
while( my $line = <>) {
    next if $line =~ /^#/;
    if( $line eq "\n" ) {
	$store->{ $section }->( $extra, \@data ) if @data;
	@data = ();
	next;
    } elsif( $line =~ /^\[/ ) {
	my($header) = $line =~ /\[([^]]+)\]/;
	( $section, $extra ) = split(":", $header);
	$define->{ $section }->( $extra );
	next;
    }

    die unless $section;

    push( @data, $line );
}

sub meta_entity {
    my $schema = shift;
    my %data = decode(shift);

    Yggdrasil::MetaEntity->admin_restore( \%data );
}

sub meta_property {
    my $schema = shift;
    my %data = decode(shift);

    Yggdrasil::MetaProperty->admin_restore( \%data );
}

sub meta_relation {
    my $schema = shift;
    my %data = decode(shift);

    Yggdrasil::MetaRelation->admin_restore( \%data );
}

sub meta_inheritance {
    my $schema = shift;
    my %data = decode(shift);

    Yggdrasil::MetaInheritance->admin_restore( \%data );
}
sub entity {
    my $schema = decode_base64( shift );
    my @data = decode(shift);

    my @visual_ids;
    for( my $i=3; $i<=@data; $i+=4 ) {
	push( @visual_ids, $data[$i] );
    }

    my $map = Yggdrasil::Entity->admin_restore( $schema, \@visual_ids );
    $ID_MAP{$schema} = $map;
}

sub property {
    my $schema = shift;
    my( $entity, $property ) = map { decode_base64($_) } split ' ', $schema;
    my %data = decode(shift, map => { id => "id" }, usemap => { id => $entity } ); 
    
    Yggdrasil::Property->admin_restore( $entity, $property, \%data );
}

sub property_define {
    my $schema = shift;
    my( $entity, $property ) = map { decode_base64($_) } split ' ', $schema;

    Yggdrasil::Property->admin_define( $entity, $property );    
}

sub relation {
    my $schema = decode_base64( shift );

    my( $e1, $e2 ) = split /_R_/, $schema;
    my %data = decode(shift, 
		      map    => { rval => "rval", lval => "lval" },
		      usemap => { lval => $e1, rval => $e2 } );
    
    Yggdrasil::Relation->admin_restore( $e1, $e2, \%data );
}

sub relation_define {
    my $schema = decode_base64( shift );

    my( $e1, $e2 ) = split /_R_/, $schema;
    Yggdrasil::Relation->admin_define( $e1, $e2 );    
}


sub decode {
    my $data = shift;
    my %param = @_;

    my $map = $param{map} || {};
    my $usemap = $param{usemap} || {};

    my @list;
    foreach my $l (@$data) {
	my( $key, $val ) = split /\s+=>\s+/, $l;
	$val = decode_base64($val);

	if( exists $map->{$key} ) {
	    $val = $ID_MAP{ $usemap->{$key} }->{$val};
	}

	$val = undef unless length $val;

	push( @list, $key, $val );
    }

    return @list;
}

sub help {
    print <<USAGE;
Usage: $0 <dump file>

Restores an Yggdrasil database from a dump file. Dump files can be
generated with ydump.

USAGE

    print <DATA>;
    exit;
}

sub version {
    print "yrestore version $VERSION\n";
    exit;
}

__DATA__
Options:
--------
--help               Prints out usage help
--version            Prints version information
--user <name>        Specify database login name
--password <pass>    Specify database password
--host <host>        Specify database host
--port <port>        Specify database port
--db <name>          Specify database name
--engine <name>      Specify database engine
--namespace <space>  Specify Yggdrasil namespace
