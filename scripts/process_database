#!/usr/bin/perl

use strict;
use warnings;

use DBI;
use Getopt::Long;

my ($zap, $dump, $bootstrap) = (0, 0);

GetOptions(
    "zap"  => \$zap,
    "dump" => \$dump,
    "help" => \&help,
    "bootstrap" => \$bootstrap,
    );

my ($engine, $db, $host, $user, $password, $port) = ($ENV{YGG_ENGINE},
						     $ENV{YGG_DB},
						     $ENV{YGG_HOST},
						     $ENV{YGG_USER},
						     $ENV{YGG_PASSWORD},
						     $ENV{YGG_PORT},
						    );
my ($dbh, $sql);

if (lc $engine eq 'mysql') {
    $sql = "SHOW TABLES";        
} elsif (lc $engine eq 'pg') {    
    $sql = "SELECT table_name FROM information_schema.tables WHERE table_schema = 'public'"
}

$dbh = DBI->connect("DBI:$engine:database=$db;host=$host;port=$port", $user, $password );

my $sth = $dbh->prepare( $sql );
die 'prepare died' unless $sth;
$sth->execute() || die 'execute died ';

while (my @row_array = $sth->fetchrow_array) {
    print "* ", $row_array[0], "\n";
    if ($dump) {
	my $table = $dbh->prepare( "select * from $row_array[0]" );
	die 'prepare died' unless $table;
	$table->execute() || die 'execute died ';

	while (my @row = $table->fetchrow_array) {
	    my @data;
	    for my $c (@row) {
		push @data, $c || 'NULL';
	    }
	    print "   ";
	    print join " | ", @data;
	    print "\n";
	}
	print "\n";
    } 
    
    if ($zap) {
	$dbh->do( "DROP TABLE $row_array[0]" );
    } elsif ($bootstrap) {
	if ($row_array[0] =~ /Meta/) {
	    $dbh->do( "TRUNCATE TABLE $row_array[0]");
	} else {
	    $dbh->do( "DROP TABLE $row_array[0]" );
	}
    }
}

sub help {
    print <DATA>;
    exit;
}

__DATA__
Usage: $0 [options]

By default display defined table names.

Options:
--------
--help        Help text
--zap         Zap the entire database
--dump        Dump all tables
--bootstrap   Remove all non-meta tables, truncate meta tables.
